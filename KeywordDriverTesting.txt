*** Settings ***
Documentation     A test suite with a single test for valid login.
...
...               This test has a workflow that is created using keywords in
...               the imported resource file.
Resource          resource.txt

*** Test Cases ***
Valid Login
    Open Browser To Login Page
    Input Username    demo
    Input Password    mode
    Submit Credentials
    Welcome Page Should Be Open
    [Teardown]    Close Browser


======================================================
    *** Settings ***
    Suite Setup       Open Browser To Login Page
    Suite Teardown    Close Browser
    Test Setup        Go To Login Page
    Test Template     Login With Invalid Credentials Should Fail
    Resource          resource.txt

    *** Test Cases ***               User Name        Password
    Invalid Username                 invalid          ${VALID PASSWORD}
    Invalid Password                 ${VALID USER}    invalid
    Invalid Username And Password    invalid          whatever
    Empty Username                   ${EMPTY}         ${VALID PASSWORD}
    Empty Password                   ${VALID USER}    ${EMPTY}
    Empty Username And Password      ${EMPTY}         ${EMPTY}

    *** Keywords ***
    Login With Invalid Credentials Should Fail
        [Arguments]    ${username}    ${password}
        Input Username    ${username}
        Input Password    ${password}
        Submit Credentials
        Login Should Have Failed

    Login Should Have Failed
        Location Should Be    ${ERROR URL}
        Title Should Be    Error Page